<?xml version="1.0" encoding="utf-8"?>
<s:BorderContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  width="100%" height="100%" creationComplete="init()">
	<s:states>
		<s:State name="hasFocus"/>
		<s:State name="lostFocus"/>
	</s:states>
	<fx:Script>
		<![CDATA[
			import com.demonsters.debugger.MonsterDebugger;
			import com.lnet.streamingvideo.data.VideoResultObject;
			import com.lnet.streamingvideo.events.ApplicationEvent;
			import com.lnet.streamingvideo.events.ApplicationEventBus;
			import com.lnet.streamingvideo.utils.KeyHandler;
			import com.lnet.streamingvideo.viewmodels.BrowseViewModel;
			
			import mx.core.FlexGlobals;
			
			private var _browseViewModel:BrowseViewModel;
			
			[Bindable]
			public function get browseViewModel():BrowseViewModel {
				return _browseViewModel;
			}

			public function set browseViewModel(value:BrowseViewModel):void {
				_browseViewModel = value;
			}
			
			private function init():void {
				MonsterDebugger.trace("BrowseView::init","CategoryList init fired...adding event listener");
				ApplicationEventBus.getInstance().addEventListener(ApplicationEvent.SET_INITIAL_CATEGORY_FOCUS, setInitialFocus, false, 0, true);
			}

			private function setInitialFocus(event:ApplicationEvent):void {
				var initialResultObj:Object = event.data;
				MonsterDebugger.trace("BrowseView::setInitialFocus","setInitialFocus event fired...attempting to set inital focus...");				
				ApplicationEventBus.getInstance().removeEventListener(ApplicationEvent.SET_INITIAL_CATEGORY_FOCUS, setInitialFocus);
				currentState = "hasFocus";
				FlexGlobals.topLevelApplication.focusManager.setFocus(categoryList);
				categoryList.selectedIndex = 0;
				browseViewModel.getCategoryResults(initialResultObj.name, initialResultObj.url);
			}

			private function navigateCategories(event:ApplicationEvent):void {
				if(event.data == "down") {
					categoryList.selectedIndex++;
				} else if (event.data == "up") {
					categoryList.selectedIndex--;
				}
			}

			private function selectionChangeHandler():void {
				browseViewModel.getCategoryResults(categoryList.selectedItem.name, categoryList.selectedItem.url);
			}

			private function keyEventHandler(e:KeyboardEvent):void {
				if (KeyHandler.keyPressed(e.keyCode) == "select") {
					currentState = "lostFocus";
					ApplicationEventBus.getInstance().dispatchEvent(new ApplicationEvent(ApplicationEvent.SET_FOCUS_TO_RESULTS));
				}
			}
		]]>
	</fx:Script>
	<mx:Image source="@Embed('assets/YouTubeLogo.png')"
			  depth="2"
			  top="30"
			  left="30"/>
	<s:BorderContainer id="overlayContainer"
					   depth="1"
					   width="316"
					   height="100%"
					   visible.hasFocus="false"
					   backgroundAlpha.hasFocus="0"
					   backgroundAlpha.lostFocus="0.5"
					   backgroundColor="0x000000"/>
	<s:List id="categoryList"
			depth="0"
			top="138"
			itemRenderer="com.lnet.streamingvideo.views.CategoryListItemRenderer"
			keyUp="keyEventHandler(event)"
			change="selectionChangeHandler()"
			dataProvider="{browseViewModel.categoryList}"
			labelField="name"
			styleName="categoryTitle"
			lineHeight="64"
			width="100%"
			height="100%"
			contentBackgroundAlpha="0"
			borderAlpha="0"/>
</s:BorderContainer>
